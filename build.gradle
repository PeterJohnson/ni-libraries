plugins {
    id 'base'
    id 'java'
    id 'maven-publish'
    id 'ru.vyarus.use-python' version '1.2.0'
}

publishing {
    repositories {
        maven {
            url "${System.getProperty('user.home')}/releases/maven/development"
        }
        maven {
            url "${System.getProperty('user.home')}/releases/maven/release"
        }
    }
}

def genNetcomm = tasks.register('generateNetcomm', PythonTask) {
    inputs.file "$rootDir/gen/wpilibj_frcnetcomm.py"
    outputs.file "$rootDir/src/main/java/edu/wpi/first/wpilibj/hal/FRCNetComm.java"

    command = "\"$rootDir/gen/wpilibj_frcnetcomm.py\""
}

compileJava.dependsOn genNetcomm

def allOutputsFolder = file("$buildDir/allOutputs")

task copyAllOutputs(type: Copy) {
    destinationDir allOutputsFolder
}

build.dependsOn copyAllOutputs

ext.addTaskToCopyAllOutputs = { task ->
    copyAllOutputs.dependsOn task
    copyAllOutputs.inputs.file task.archivePath
    copyAllOutputs.from task.archivePath
}

def pubVersion = '2018.17.0'

def baseArtifactId = 'chipobject'
def artifactGroupId = 'edu.wpi.first.ni-libraries'
def zipBaseName = '_GROUP_edu_wpi_first_ni-libraries_ID_chipobject_CLS'

def netcommBaseArtifactId = 'netcomm-cpp'
def netcommZipBaseName = '_GROUP_edu_wpi_first_ni-libraries_ID_netcomm-cpp_CLS'

def netcommJavaBaseArtifactId = 'netcomm-java'
def netcommJavaZipBaseName = '_GROUP_edu_wpi_first_ni-libraries_ID_netcomm-java_CLS'

def outputsFolder = file("$project.buildDir/outputs")

def versionFile = file("$outputsFolder/version.txt")

task outputVersions() {
    description = 'Prints the versions of this to a file for use by the downstream packaging project'
    group = 'Build'
    outputs.files(versionFile)

    doFirst {
        buildDir.mkdir()
        outputsFolder.mkdir()
    }

    doLast {
        versionFile.write pubVersion
    }
}

build.dependsOn outputVersions
copyAllOutputs.dependsOn outputVersions
copyAllOutputs.inputs.file versionFile
copyAllOutputs.from versionFile

task libZip(type: Zip) {
    destinationDir = outputsFolder
    baseName = zipBaseName
    classifier = "linuxathena"

    from('src/lib/chipobject') {
        into '/linux/athena/shared/'
    }
}

task headersZip(type: Zip) {
    destinationDir = outputsFolder
    baseName = zipBaseName
    classifier = "headers"

    from('src/include/FRC_FPGA_ChipObject') {
        into '/FRC_FPGA_ChipObject'
    }
}

task netCommLib(type: Zip) {
    destinationDir = outputsFolder
    baseName = netcommZipBaseName
    classifier = "linuxathena"

    from('src/lib/netcomm') {
        into '/linux/athena/shared/'
    }
}

task netCommHeaders(type: Zip) {
    destinationDir = outputsFolder
    baseName = netcommZipBaseName
    classifier = "headers"

    from('src/include/FRC_NetworkCommunication') {
        into '/FRC_NetworkCommunication'
    }
}

task outputJar(type: Jar, dependsOn: classes) {
    baseName netcommJavaZipBaseName
    destinationDir outputsFolder
    from sourceSets.main.output
}

build.dependsOn netCommLib
build.dependsOn headersZip
build.dependsOn netCommHeaders
build.dependsOn libZip
build.dependsOn outputJar

addTaskToCopyAllOutputs(netCommLib)
addTaskToCopyAllOutputs(headersZip)
addTaskToCopyAllOutputs(libZip)
addTaskToCopyAllOutputs(netCommHeaders)
addTaskToCopyAllOutputs(outputJar)

publishing {
    publications {
        chipobject(MavenPublication) {
            artifact libZip
            artifact headersZip

            artifactId = "${baseArtifactId}"
            groupId artifactGroupId
            version pubVersion
        }
        netcommCpp(MavenPublication) {
            artifact netCommLib
            artifact netCommHeaders

            artifactId = "${netcommBaseArtifactId}"
            groupId artifactGroupId
            version pubVersion
        }
        netcommJava(MavenPublication) {
            artifact outputJar

            artifactId = "${netcommJavaBaseArtifactId}"
            groupId artifactGroupId
            version pubVersion
        }
    }
}

task patchNiLibraries() {
    doLast {
        // Patch ChipObject headers to be self contained
        FileTree chipTree = fileTree(dir: "$rootDir/src/include/FRC_FPGA_ChipObject/nRoboRIO_FPGANamespace")
        chipTree.each { File file ->
            String contents = file.getText('UTF-8')
            contents = contents.replaceAll('#include \"tSystemInterface.h\"', '#include \"../tSystem.h\"\n#include \"../tSystemInterface.h\"')
            file.write(contents, 'UTF-8')
        }

        // Patch NetComm headers to work on Windows
        FileTree netTree = fileTree(dir: "$rootDir/src/include/FRC_NetworkCommunication")
        netTree.each { File file ->
            String contents = file.getText('UTF-8')
            contents = contents.replaceAll('#ifdef WIN32', '#ifdef _WIN32')
            contents = contents.replaceAll('# include <vxWorks_compat.h>', '#include <windows.h>')
            contents = contents.replaceAll('#include <vxWorks_compat.h>', '#include <windows.h>')
            file.write(contents, 'UTF-8')
        }

        FileTree allTree = fileTree(dir: "$rootDir/src/include/")
        allTree.each { File file ->
            String contents = file.getText('UTF-8')
            contents = contents.replaceAll('\r\n', '\n')
            file.write(contents, 'UTF-8')
        }
    }
}

wrapper {
  gradleVersion = '4.9'
}
